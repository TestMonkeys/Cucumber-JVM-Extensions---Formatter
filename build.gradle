group 'org.testmonkeys'
version '0.1.4'

allprojects {
    repositories {
        jcenter()
    }
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java'
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'org.testmonkeys.cucumber.ext.formatters'

    publishedGroupId = 'org.testmonkeys'
    libraryName = 'CucumberJVM Extended Formatters'
    artifact = 'cukejvm-ext-formatter'

    libraryDescription = 'Custom formatters for Cucumber JVM'

    siteUrl = 'https://github.com/TestMonkeys/Cucumber-JVM-Extensions---Formatter'
    gitUrl = 'https://github.com/TestMonkeys/Cucumber-JVM-Extensions---Formatter.git'

    libraryVersion = version

    developerId = 'costea32'
    developerName = 'Constantin Pascal'
    developerEmail = 'costea32@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
    }
}
apply plugin: 'com.jfrog.bintray'
sourceCompatibility = 1.8

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

//jar.dependsOn sourcesJar

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "costea32"
            name "Constantin Pascal"
            email "costea32@gmail.com"
        }
    }
    scm{
        connection "scm:git:git@github.com/TestMonkeys/Cucumber-JVM-Extensions---Formatter.git"
        developerConnection "scm:git:git@github.com/TestMonkeys/Cucumber-JVM-Extensions---Formatter.git"
        url "git@github.com/TestMonkeys/Cucumber-JVM-Extensions---Formatter.git"
    }
}


publishing {
    publications {
        MavenPub(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            groupId 'org.testmonkeys'
            artifactId 'cukejvm-ext-formatter'
            version libraryVersion

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Cucumber JVM extended formatters')
                root.appendNode('name',libraryName)
                root.appendNode('url',siteUrl)
                root.children().last() + pomConfig
            }
        }
    }

}

// Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications =['MavenPub']
    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        userOrg = 'testmonkeys'
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = false
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = properties.getProperty("bintray.gpg.password")
                //Optional. The passphrase for GPG signing'
            }
        }
    }
}



repositories {
    mavenCentral()
}

dependencies {
    compile 'info.cukes:gherkin:2.12.2','info.cukes:gherkin-jvm-deps:1.0.3'
    testCompile 'junit:junit:4.11',
            'info.cukes:cucumber-java:1.2.4',
            'info.cukes:cucumber-junit:1.2.4'
}

